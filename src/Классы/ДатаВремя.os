#Использовать logos

Перем Лог;
Перем ДатаНачалаОтсчета;

// Конвертирует текущую дату в формат метки времени (timestamp)
//
//  Возвращаемое значение:
//   число - дата в формате unix timestamp
//
Функция ТекущаяДатаВМеткуВремени() Экспорт

	Возврат ВМеткуВремени(ТекущаяДата());

КонецФункции

// Конвертирует дату в формат метки времени (timestamp)
//
// Параметры:
//   ВходящаяДата - дата - дата для конвертации
//
//  Возвращаемое значение:
//   число - дата в формате unix timestamp
//
Функция ВМеткуВремени(ВходящаяДата) Экспорт

	Возврат Цел(ВходящаяДата - ДатаНачалаОтсчета);

КонецФункции

// Конвертирует строку в дату по шаблону форматной строки
//
// Параметры
//  Строка		- строка, содержащая дату,
//  ФорматДаты	- форматная строка вида:
// [dd MM yy]
// [yy-MM-dd HH:mm]
// [dd MM yy HH mm ss]
// [dd MM yyyy]
// [MM dd, yyyy, HH:mm]
// [dd.MM.yy]
// [MM, dd, yyyy]
// [yyyyMMdd]
// [HH-mm-ss, dd-MM-yy]
// [dd]
// [dd MM HH:mm:ss yyyy]
// [HH:mm:ss]
// [HH:mm]
// [yyyy-MM-dd HH:mm:ss]
// [yyyy-MM-dd]
// [yyyy/MM/dd]
// [dd/MM/yy]
// [MM dd yyyy]
// [yyyy-MM]
// [yyyy]
// [MM, dd, yyyy]
// [dd-MM-yyyy]
// [ddMMyyyy]
//
Функция СтрокаВДату(Знач Строка, ФорматДаты) Экспорт
	Ч = Новый Соответствие; 
	Для ё = 1 По СтрДлина(ФорматДаты) + 6 Цикл 
	    Ч[Сред(ФорматДаты + "dMyHms", ё, 1)] = 0
	КонецЦикла; 
	Для ё = 1 По СтрДлина(ФорматДаты) Цикл 
	    Ч[Сред(ФорматДаты, ё, 1)] = 10 * Ч[Сред(ФорматДаты, ё, 1)] + Найти("123456789", Сред(Строка, ё, 1));
	КонецЦикла; 
	Ч["y"] = Ч["y"] + ?(0 < Ч["y"] И Ч["y"] < 50, 2000, ?(0 < Ч["y"] И Ч["y"] < 100, 1900, 0));
	Возврат Дата(Макс(Ч["y"], 1), Макс(Ч["M"],1), Макс(Ч["d"],1), Ч["H"], Ч["m"], Ч["s"]);
КонецФункции 

// Конвертирует дату из формата метки времени (timestamp) в формат дата
//
// Параметры:
//   УниверсальноеВремяВМиллисекундах - Число - дата в миллисекундах ( timestamp
//
//  Возвращаемое значение:
//   Дата - дата в нормальном формате
//
Функция ИзМеткиВремени(УниверсальноеВремяВМиллисекундах) Экспорт

	Возврат ДатаВремяИзУниверсальногоФормата(УниверсальноеВремяВМиллисекундах);
	
КонецФункции

Функция ДатаВремяИзУниверсальногоФормата(УниверсальноеВремяВМиллисекундах) 
                
	УниверсальнаяДата_Число = УниверсальноеВремяВМиллисекундах;
	УниверсальнаяДата_Дата = ДатаНачалаОтсчета + УниверсальнаяДата_Число;    
	
	Возврат УниверсальнаяДата_Дата;

КонецФункции

Процедура Инициализация()
	
	Лог = Логирование.ПолучитьЛог("oscript.lib.time");
	Год1970 = 1970;
	ДатаНачалаОтсчета = Дата(Год1970, 1, 1, 0, 0, 0);

КонецПроцедуры

Инициализация();